%class FSMSolution
%header FSMSolution.h

%start FSMMap::Init

%map FSMMap
%%
Init
{
  symb(c : char)  [ (c >= 'a' && c <= 'z') ||
                    (c >= '0' && c <= '9') ||
                    (c == '_') || (c == '+') ||
                    (c == '-') || (c == '.') ]
                    Name  {}
}

Name
{
  symb(c : char)  [ (c >= 'a' && c <= 'z') ||
                    (c >= '0' && c <= '9') ||
                    (c == '_') || (c == '+') ||
                    (c == '-') || (c == '.') ]
                    nil  {}
  symb(c : char)  [ c == '|']
                    Bar_size  {}
}

Bar_size
{
  symb(c : char)  [ c >= '0' && c <= '9' ]
                    Size  {}
}

Size
{
  symb(c : char)  [ c >= '0' && c <= '9' ]
                    nil {}
  symb(c : char)  [ c == '|' ]
                    Bar_hash  {}
}

Bar_hash
{
  symb(c : char) [ (c >= 'a' && c <= 'f') ||
                    (c >= '0' && c <= '9') ]
                    Hash {}
}

Hash
{
  symb(c : char) [ (c >= 'a' && c <= 'f') ||
                    (c >= '0' && c <= '9') ]
                    nil {}
  symb(c : char)  [ c == '|' ]
                    Bar_end  {}
}

Bar_end
{
  symb(c : char)  [ c == '/' ]
                    OK  {Acceptable();}
}

OK
{}

Error
{
  symb(c : char)
                  nil {}
}

Default
{
  EOS             Error  {Unacceptable();}
  symb(c : char)  Error  {Unacceptable();}
}
%%
